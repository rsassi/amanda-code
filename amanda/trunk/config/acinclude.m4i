dnl Check if the compiler can handle unsigned long constants, ie 2ul.
AC_DEFUN([AMANDA_C_UNSIGNED_LONG_CONSTANTS],
    [
	AC_CACHE_CHECK(
	    [for working unsigned long constants],
	    amanda_cv_c_unsigned_long_constants,
	    [
		AC_TRY_COMPILE(
		    [
		    ],
		    [
			long l = 1ul;
		    ],
		    amanda_cv_c_unsigned_long_constants=yes,
		    amanda_cv_c_unsigned_long_constants=no
		)
	    ]
	)
	if test "$amanda_cv_c_unsigned_long_constants" = yes; then
	    AC_DEFINE(HAVE_UNSIGNED_LONG_CONSTANTS,1,[Define if the compiler support unsigned long constants. ])
	fi
    ]
)

dnl Check for the argument type for shmat() and shmdt()
AC_DEFUN([AMANDA_FUNC_SHM_ARG_TYPE],
    [
	AC_CACHE_CHECK(
	    [for shmdt() argument type],
	    amanda_cv_shmdt_arg_type,
	    [
		if test "$ac_cv_func_shmget" = yes; then
		    cat <<EOF >conftest.$ac_ext
#include "confdefs.h"
#ifdef HAVE_SYS_TYPES_H
# include <sys/types.h>
#endif
#ifdef HAVE_SYS_IPC_H
# include <sys/ipc.h>
#endif
#ifdef HAVE_SYS_SHM_H
# include <sys/shm.h>
#endif

#ifdef __cplusplus
extern "C" void *shmat(int, void *, int);
#else
void *shmat();
#endif

int main()
{
    int i;
    return 0;
}
EOF
		    ${CC-cc} -c $CFLAGS $CPPFLAGS conftest.$ac_ext >/dev/null 2>/dev/null
		    if test $? = 0; then
			amanda_cv_shmdt_arg_type=void
		    else
			amanda_cv_shmdt_arg_type=char
		    fi
		    rm -f conftest*
		else
		    amanda_cv_shmdt_arg_type=nothing
		fi
	    ]
	)
	AC_DEFINE_UNQUOTED(SHM_ARG_TYPE,$amanda_cv_shmdt_arg_type,[Define to type of shmget() function argument. ])
    ]
)

dnl Figure out the select() argument type.
AC_DEFUN([AMANDA_FUNC_SELECT_ARG_TYPE],
    [
	AC_CACHE_CHECK(
	    [for select() argument type],
	    amanda_cv_select_arg_type,
	    [
		rm -f conftest.c
		cat <<EOF >conftest.$ac_ext
#include "confdefs.h"
#ifdef HAVE_SYS_TIME_H
# include <sys/time.h>
#endif
#ifdef HAVE_SYS_TYPES_H
# include <sys/types.h>
#endif
#ifdef HAVE_SYS_SELECT_H
#  include <sys/select.h>
#endif
#ifdef HAVE_SYS_SOCKET_H
#  include <sys/socket.h>
#endif
#ifdef HAVE_UNISTD_H
# include <unistd.h>
#endif

int main()
{
#ifdef FD_SET_POINTER
	(void)select(0, (fd_set *) 0, (fd_set *) 0, (fd_set *) 0, 0);
#else
	(void)select(0, (int *) 0, (int *) 0, (int *) 0, 0);
#endif
	return 0;
}
EOF

		dnl Figure out the select argument type by first trying to
		dnl compile with the fd_set argument.  If the compile fails,
		dnl then we know to use the int.  If it suceeds, then try to
		dnl use the int.  If the int fails, then use fd_set.  If
		dnl both suceeed, then do a line count on the number of
		dnl lines that the compiler spit out, assuming that the
		dnl compile outputing more lines had more errors.
		amanda_cv_select_arg_type=no
		select_compile="${CC-cc} -c $CFLAGS $CPPFLAGS"
		$select_compile -DFD_SET_POINTER conftest.$ac_ext 1>conftest.fd_set 2>&1
		if test $? -ne 0; then
		    amanda_cv_select_arg_type=int
		fi
		if test "$amanda_cv_select_arg_type" = no; then
		    $select_compile conftest.$ac_ext 1>conftest.int 2>&1
		    if test $? -ne 0; then
			amanda_cv_select_arg_type=fd_set
		    fi
		fi
		if test "$amanda_cv_select_arg_type" = no; then
		    wc_fdset=`wc -l <conftest.fd_set`
		    wc_int=`wc -l <conftest.int`
		    if test "$wc_fdset" -le "$wc_int"; then
			amanda_cv_select_arg_type=fd_set
		    else
			amanda_cv_select_arg_type=int
		    fi
		fi
		rm -f conftest*
	    ]
	)
	AC_DEFINE_UNQUOTED(SELECT_ARG_TYPE,$amanda_cv_select_arg_type,[Define to type of select arguments. ])
    ]
)

dnl Check if setsockopt can use the SO_SNDTIMEO option.
dnl This defines HAVE_SO_SNDTIMEO if setsockopt works
dnl with SO_SNDTIMEO.
AC_DEFUN([AMANDA_FUNC_SETSOCKOPT_SO_SNDTIMEO],
    [
	AC_CACHE_CHECK(
	    [for setsockopt SO_SNDTIMEO option],
	    amanda_cv_setsockopt_SO_SNDTIMEO,
	    [
		AC_TRY_RUN(
		    [
#include <sys/types.h>
#include <sys/socket.h>
#ifdef TIME_WITH_SYS_TIME
#  include <sys/time.h>
#  include <time.h>
#else
#  ifdef HAVE_SYS_TIME_H
#    include <sys/time.h>
#  else
#    include <time.h>
#  endif
#endif

main() {
#ifdef SO_SNDTIMEO
    int sock = socket(AF_INET, SOCK_STREAM, 0);
    struct timeval timeout;
    timeout.tv_sec = 1;
    timeout.tv_usec = 0;
    return (setsockopt(sock, SOL_SOCKET, SO_SNDTIMEO,
             (void *)&timeout, sizeof(timeout)));
#else
    return -1;
#endif
}
		    ],
		    amanda_cv_setsockopt_SO_SNDTIMEO=yes,
		    amanda_cv_setsockopt_SO_SNDTIMEO=no,
		    amanda_cv_setsockopt_SO_SNDTIMEO=no
		)
	    ]
	)
	if test "$amanda_cv_setsockopt_SO_SNDTIMEO" = yes; then
	    AC_DEFINE(HAVE_SO_SNDTIMEO,1,[Define if SO_SNDTIMEO is available. ])
	fi
    ]
)

dnl Check for the one or two argument version of gettimeofday.
AC_DEFUN([AMANDA_FUNC_GETTIMEOFDAY_ARGS],
    [
	AC_REQUIRE([AC_HEADER_TIME])
	AC_CACHE_CHECK(
	    [for gettimeofday number of arguments],
	    amanda_cv_gettimeofday_args,
	    [
		AC_TRY_COMPILE(
		    [
#ifdef TIME_WITH_SYS_TIME
#  include <sys/time.h>
#  include <time.h>
#else
#  ifdef HAVE_SYS_TIME_H
#    include <sys/time.h>
#  else
#    include <time.h>
#  endif
#endif
		    ],
		    [
			struct timeval val;
			struct timezone zone;
			gettimeofday(&val, &zone);
		    ],
		    amanda_cv_gettimeofday_args=2,
		    amanda_cv_gettimeofday_args=1
		)
	    ]
	)
	if test "$amanda_cv_gettimeofday_args" = 2; then
	    AC_DEFINE(HAVE_TWO_ARG_GETTIMEOFDAY,1,[Define if gettimeofday takes two arguments. ])
	fi
    ]
)



dnl Check for if pid_t is a long, int, or short.
AC_DEFUN([AMANDA_TYPE_PID_T],
    [
	AC_REQUIRE([AC_TYPE_PID_T])
	AC_CACHE_CHECK([for pid_t type], amanda_cv_pid_type,
	    [
		amanda_cv_pid_type=unknown
		if test "$ac_cv_type_pid_t" = no; then
		    amanda_cv_pid_type=int
		fi
		for TEST_amanda_cv_pid_type in long short int; do
		    if test $amanda_cv_pid_type = unknown; then
			AC_EGREP_CPP(typedef.*${TEST_amanda_cv_pid_type}.*pid_t,
			    [
#include <sys/types.h>
#if STDC_HEADERS
#include <stdlib.h>
#include <stddef.h>
#endif
			    ],
			amanda_cv_pid_type=$TEST_amanda_cv_pid_type)
		    fi
		    if test $amanda_cv_pid_type = unknown; then
			AC_EGREP_CPP(ZZZZ.*${TEST_amanda_cv_pid_type},
			    [
#include <sys/types.h>
#if STDC_HEADERS
#include <stdlib.h>
#include <stddef.h>
#endif
				ZZZZ pid_t
			],
			amanda_cv_pid_type=$TEST_amanda_cv_pid_type)
		    fi
		done
		if test $amanda_cv_pid_type = unknown; then
		    amanda_cv_pid_type=int
		fi
	    ]
	)
	case $amanda_cv_pid_type in
	    int)	AC_DEFINE_UNQUOTED(PRINTF_PID_T,"%d",[Define to printf formatting string to print a PID. ]) ;;
	    long)	AC_DEFINE_UNQUOTED(PRINTF_PID_T,"%ld") ;;
	    short)	AC_DEFINE_UNQUOTED(PRINTF_PID_T,"%d") ;;
	esac
    ]
)

dnl
dnl
dnl ICE_CHECK_DECL (FUNCTION, HEADER-FILE...)
dnl If FUNCTION is available, define `HAVE_FUNCTION'.  If it is declared
dnl in one of the headers named in the whitespace-separated list 
dnl HEADER_FILE, define `HAVE_FUNCTION_DECL` (in all capitals).
dnl
AC_DEFUN([ICE_CHECK_DECL],
[
ice_have_$1=no
AC_CHECK_FUNCS($1, ice_have_$1=yes)
if test "${ice_have_$1}" = yes; then
AC_MSG_CHECKING(for $1 declaration in $2)
AC_CACHE_VAL(ice_cv_have_$1_decl,
[
ice_cv_have_$1_decl=no
changequote(,)dnl
ice_re_params='[a-zA-Z_][a-zA-Z0-9_]*'
ice_re_word='(^|[^a-zA-Z0-9_])'
changequote([,])dnl
for header in $2; do
# Check for ordinary declaration
AC_EGREP_HEADER([${ice_re_word}$1[ 	]*\(], $header, 
	ice_cv_have_$1_decl=yes)
if test "$ice_cv_have_$1_decl" = yes; then
	break
fi
# Check for "fixed" declaration like "getpid _PARAMS((int))"
AC_EGREP_HEADER([${ice_re_word}$1[ 	]*$ice_re_params\(\(], $header, 
	ice_cv_have_$1_decl=yes)
if test "$ice_cv_have_$1_decl" = yes; then
	break
fi
done
])
AC_MSG_RESULT($ice_cv_have_$1_decl)
if test "$ice_cv_have_$1_decl" = yes; then
AC_DEFINE_UNQUOTED([HAVE_]translit($1,[a-z],[A-Z])[_DECL],1,[Define if $1 is declared. ])
fi
fi
])dnl
dnl Test for the presence of <sys/wait.h>, 'union wait', arg-type of 'wait()'.
dnl by T.E.Dickey" , Jim Spath <jspath@mail.bcpl.lib.md.us>
dnl
dnl     FIXME: These tests should have been in autoconf 1.11!
dnl
dnl     Note that we cannot simply grep for 'union wait' in the wait.h file,
dnl     because some Posix systems turn this on only when a BSD variable is
dnl     defined. Since I'm trying to do without special defines, I'll live
dnl     with the default behavior of the include-file.
dnl
dnl     I do _2_ compile checks, because we may have union-wait, but the
dnl     prototype for 'wait()' may want an int.
dnl
dnl     Don't use HAVE_UNION_WAIT, because the autoconf documentation implies
dnl     that if we've got union-wait, we'll automatically use it.
dnl
dnl Garrett Wollman adds:
dnl	The tests described above don't quite do the right thing,
dnl	since some systems have hacks which allow `union wait' to
dnl	still work even though `int' is preferred (and generates
dnl	fewer warnings).  Since all of these systems use prototypes,
dnl	we can use the prototype of wait(2) to disambiguate them.
dnl
dnl Alexandre Oliva adds:
dnl     A single compile check is enough.  If we don't have union wait,
dnl     it's obvious that the test will fail, and that we must use int.
dnl     If we do, the prototype (on STDC systems) and WIFEXITED will tell
dnl     whether we're supposed to be using union wait instead of int.
dnl
AC_DEFUN([CF_WAIT],
[
AC_REQUIRE([AC_TYPE_PID_T])
AC_HAVE_HEADERS(sys/wait.h wait.h)
AC_CACHE_CHECK([whether wait uses union wait], [cf_cv_arg_union_wait],
        [AC_TRY_COMPILE([
#include <sys/types.h>

#if HAVE_SYS_WAIT_H
# include <sys/wait.h>
#else
# if HAVE_WAIT_H
#  include <wait.h>
# endif
#endif

#ifdef __STDC__
pid_t wait(union wait *);
#endif
], [
  union wait x; int i;
  wait(&x); i = WIFEXITED(x)
], [cf_cv_arg_union_wait=yes], [cf_cv_arg_union_wait=no])])
if test $cf_cv_arg_union_wait = yes; then
	AC_DEFINE(WAIT_USES_UNION,1,[Defined if wait() puts the status in a union wait instead of int. ])
fi
])dnl
AC_DEFUN([CF_WAIT_INT],
[
AC_REQUIRE([AC_TYPE_PID_T])
AC_HAVE_HEADERS(sys/wait.h wait.h)
AC_CACHE_CHECK([whether wait uses int], [cf_cv_arg_int],
        [AC_TRY_COMPILE([
#include <sys/types.h>

#if HAVE_SYS_WAIT_H
# include <sys/wait.h>
#else
# if HAVE_WAIT_H
#  include <wait.h>
# endif
#endif

#ifdef __STDC__
pid_t wait(int *);
#endif
], [
  int x; int i;
  wait(&x); i = WIFEXITED(x)
], [cf_cv_arg_int=yes], [cf_cv_arg_int=no])])
if test $cf_cv_arg_int = yes; then
        AC_DEFINE(WAIT_USES_INT,1,[Defined if wait() puts the status in a int instead of a union wait. ])
fi
])dnl

# AC_PROG_XSLTPROC
# -----------------------------------------------------------------
# Find an xsltproc executable.
#
# Input:
#  $1 is the default $XSLTPROC_FLAGS, which will be overridden if the
#  user specifies --with-xsltproc-flags.
# Output:
#  $XSLTPROC contains the path to xsltproc, or is empty if none was
#  found or the user specified --without-xsltproc. $XSLTPROC_FLAGS 
#  contains the flags to use with xsltproc.

AC_DEFUN([AC_PROG_XSLTPROC],
[
XSLTPROC_FLAGS="$1"
AC_SUBST(XSLTPROC_FLAGS)

# The (lack of) whitespace and overquoting here are all necessary for
# proper formatting.
AC_ARG_WITH(xsltproc,
AS_HELP_STRING([--with-xsltproc[[[[[=PATH]]]]]],
               [Use the xsltproc binary in in PATH.]),
    [ ac_with_xsltproc=$withval; ],
    [ ac_with_xsltproc=maybe; ])

AC_ARG_WITH(xsltproc-flags,
AS_HELP_STRING([  --with-xsltproc-flags=FLAGS],
               [Flags to pass to xsltproc (default $1)]),
    [ if test "x$withval" == "xno"; then
	XSLTPROC_FLAGS=''
    else
	if test "x$withval" != "xyes"; then
	    XSLTPROC_FLAGS="$withval"
	fi
    fi
	])

# search for xsltproc if it wasn't specified
if test "$ac_with_xsltproc" = "yes" -o "$ac_with_xsltproc" = "maybe"; then
    AC_PATH_PROGS(XSLTPROC,xsltproc,,$LOCSYSPATH)
else
    if test "$ac_with_xsltproc" != "no"; then
        if test -x "$ac_with_xsltproc"; then
            XSLTPROC="$ac_with_xsltproc";
        else
            AC_MSG_WARN([Specified xsltproc of $ac_with_xsltproc isn't])
            AC_MSG_WARN([executable; searching for an alternative.])
            AC_PATH_PROGS(XSLTPROC,xsltproc,,$LOCSYSPATH)
        fi
    fi
fi
])

# AC_CHECK_DOCBOOK_XSLT
# -----------------------------------------------------------------
# Check for access to docbook stylesheets of a particular revision.
# This macro can be used for multiple versions within the same script.
#
# Input:
#  $1 is the version of docbook to search for; default 'current'
# Output:
#  $HAVE_DOCBOOK_XSLT_VERS will be set to 'yes' or 'no' depending
#  on the results of the test, where VERS is $1, with '_' substituted
#  for '.'  $HAVE_DOCBOOK_XSLT will also be set to the same value.
AC_DEFUN([AC_CHECK_DOCBOOK_XSLT],
[
    AC_REQUIRE([AC_PROG_XSLTPROC])

    dnl define a temporary variable for the version, so this macro can be
    dnl used with multiple versions
    define([_VERS], $1)
    ifelse(_VERS, [], [define([_VERS], [current])])
    define([ac_cv_docbook_xslt_VERS], patsubst([ac_cv_docbook_xslt_]_VERS, [\.], [_]))
    define([HAVE_DOCBOOK_XSLT_VERS], patsubst([HAVE_DOCBOOK_XSLT_]_VERS, [\.], [_]))

    AC_CACHE_CHECK([for Docbook XSLT version ]_VERS, [ac_cv_docbook_xslt_VERS],
    [
	ac_cv_docbook_xslt_VERS=no
	if test -n "$XSLTPROC"; then
	    echo "Trying '$XSLTPROC $XSLTPROC_FLAGS http://docbook.sourceforge.net/release/xsl/_VERS/xhtml/docbook.xsl'" >&AS_MESSAGE_LOG_FD
	    $XSLTPROC $XSLTPROC_FLAGS http://docbook.sourceforge.net/release/xsl/_VERS/xhtml/docbook.xsl >&AS_MESSAGE_LOG_FD 2>&1

	    if test "$?" = 0; then
		ac_cv_docbook_xslt_VERS=yes
	    fi
	fi
    ])

    HAVE_DOCBOOK_XSLT_VERS="$ac_cv_docbook_xslt_VERS"
    HAVE_DOCBOOK_XSLT=HAVE_DOCBOOK_XSLT_VERS
    undefine([_VERS])
])

# AC_CHECK_DOCBOOK_DTD
# -----------------------------------------------------------------
# Check for access to docbook DTD of a particular revision.
# This macro can be used for multiple versions within the same script.
#
# Input:
#  $1 is the version of docbook to search for; default 'current'
# Output:
#  $HAVE_DOCBOOK_DTD_VERS will be set to 'yes' or 'no' depending
#  on the results of the test, where VERS is $1, with '_' substituted
#  for '.'  $HAVE_DOCBOOK_DTD will also be set to the same value.
AC_DEFUN([AC_CHECK_DOCBOOK_DTD],
[
    AC_REQUIRE([AC_PROG_XSLTPROC])

    dnl define a temporary variable for the version, so this macro can be
    dnl used with multiple versions
    define([_VERS], $1)
    ifelse(_VERS, [], [define([_VERS], [current])])
    define([ac_cv_docbook_dtd_VERS], patsubst([ac_cv_docbook_dtd_]_VERS, [\.], [_]))
    define([HAVE_DOCBOOK_DTD_VERS], patsubst([HAVE_DOCBOOK_DTD_]_VERS, [\.], [_]))

    AC_CACHE_CHECK([for Docbook DTD version ]_VERS, [ac_cv_docbook_dtd_VERS],
    [
	ac_cv_docbook_dtd_VERS=no
	if test -n "$XSLTPROC"; then
	    MY_XSLTPROC_FLAGS=`echo "" $XSLTPROC_FLAGS|sed -e s/--novalid//g`
	    cat <<EOF >conftest.xml
<?xml version="1.0" encoding='ISO-8859-1'?>
<!DOCTYPE book PUBLIC "-//OASIS//DTD DocBook XML V[]_VERS//EN" "http://www.oasis-open.org/docbook/xml/_VERS/docbookx.dtd">
<book id="empty">
</book>
EOF
	    echo "Trying '$XSLTPROC $MY_XSLTPROC_FLAGS conftest.xml'" >&AS_MESSAGE_LOG_FD
	    $XSLTPROC $MY_XSLTPROC_FLAGS conftest.xml >conftest.out 2>&1
	    if test "$?" = 0; then
		# failing to load the DTD is just a warning, so check for it in the output.
		if grep 'warning: failed to load external entity' conftest_out >/dev/null 2>&1; then
		    : # no good..
		else
		    ac_cv_docbook_dtd_VERS=yes
		fi
	    fi
	    cat conftest.out >&AS_MESSAGE_LOG_FD

	    rm -f conftest.xml conftest.out
	fi
    ])

    HAVE_DOCBOOK_DTD_VERS="$ac_cv_docbook_dtd_VERS"
    HAVE_DOCBOOK_DTD=HAVE_DOCBOOK_DTD_VERS
    undefine([_VERS])
])

dnl
dnl Checks to see if there's a sockaddr_storage structure
dnl
dnl usage:
dnl
dnl	AC_SOCKADDR_STORAGE
dnl
dnl results:
dnl
dnl	HAVE_SOCKADDR_STORAGE (defined)
dnl
AC_DEFUN([AC_SOCKADDR_STORAGE],
    [AC_MSG_CHECKING(if sockaddr_storage struct exists)
    AC_CACHE_VAL(ac_cv_has_sockaddr_storage,
	AC_TRY_COMPILE([
#	include <sys/types.h>
#	include <sys/socket.h>],
	[u_int i = sizeof (struct sockaddr_storage)],
	ac_cv_has_sockaddr_storage=yes,
	ac_cv_has_sockaddr_storage=no))
    AC_MSG_RESULT($ac_cv_has_sockaddr_storage)
    if test $ac_cv_has_sockaddr_storage = yes ; then
	    AC_DEFINE(HAVE_SOCKADDR_STORAGE,1,[if struct sockaddr_storage exists])
    fi])

